/*
 * Copyright 2020-2023 Slawomir Czerwinski
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package it.czerwinski.intellij.wavefront.lang;

import com.intellij.lexer.FlexLexer;
import com.intellij.psi.TokenType;
import com.intellij.psi.tree.IElementType;
import it.czerwinski.intellij.wavefront.lang.psi.ObjSpecialTypes;
import it.czerwinski.intellij.wavefront.lang.psi.ObjTypes;

%%

%class ObjLexer
%implements FlexLexer
%unicode
%function advance
%type IElementType
%eof{  return;
%eof}

CRLF=[\r\n\f]+

COMMENT_SYMBOL="#"[\ \t]*?

WHITE_SPACE=[\ \t]+

OBJECT_KEYWORD="o"
GROUP_KEYWORD="g"

VERTEX_KEYWORD="v"
TEXTURE_COORDINATES_KEYWORD="vt"
VERTEX_NORMAL_KEYWORD="vn"

FACE_KEYWORD="f"
LINE_KEYWORD="l"
POINT_KEYWORD="p"

INDEX_SEPARATOR=\/

FREE_FORM_POINT_KEYWORD="vp"
FREE_FORM_TYPE_KEYWORD="cstype"
FREE_FORM_RATIONAL_KEYWORD="rat"
FREE_FORM_DEGREE_KEYWORD="deg"
FREE_FORM_BASIS_MATRIX_KEYWORD="bmat"
FREE_FORM_STEP_SIZE_KEYWORD="step"
FREE_FORM_CURVE_KEYWORD="curv"
FREE_FORM_2D_CURVE_KEYWORD="curv2"
FREE_FORM_SURFACE_KEYWORD="surf"
FREE_FORM_PARAMETERS_KEYWORD="parm"
FREE_FORM_OUTER_TRIMMING_LOOP_KEYWORD="trim"
FREE_FORM_INNER_TRIMMING_LOOP_KEYWORD="hole"
FREE_FORM_SPECIAL_CURVE_KEYWORD="scrv"
FREE_FORM_SPECIAL_POINTS_KEYWORD="sp"
FREE_FORM_BODY_END_KEYWORD="end"

SMOOTHING_GROUP_KEYWORD="s"
SMOOTHING_GROUP_NUMBER=([1-9][\d]*)|((0)|(off))

MATERIAL_FILE_REF_KEYWORD="mtllib"
MATERIAL_REFERENCE_KEYWORD="usemtl"

OBJECT_OR_GROUP_NAME=[^\ \t\r\n\f]+
NUMBER="-"?((0)|([1-9][\d]*))("."[\d]+)?([Ee][+-]?[\d]+)?
FREE_FORM_DIRECTION=(u)|(v)
FREE_FORM_TYPE_NAME=(bmatrix)|(bezier)|(bspline)|(cardinal)|(taylor)
REFERENCE=[^\ \t\r\n\f]+

%state WAITING_COMMENT
%state WAITING_OBJECT_OR_GROUP_NAME
%state WAITING_NUMBER_OR_INDEX
%state WAITING_FREE_FORM_TYPE_NAME
%state WAITING_FREE_FORM_DIRECTION
%state WAITING_SMOOTHING_GROUP_NUMBER
%state WAITING_MATERIAL_FILE_NAME
%state WAITING_MATERIAL_NAME
%state END

%state INVALID

%%

<YYINITIAL> {COMMENT_SYMBOL} { yybegin(WAITING_COMMENT); return ObjTypes.COMMENT_SYMBOL; }

<YYINITIAL> {OBJECT_KEYWORD} { yybegin(WAITING_OBJECT_OR_GROUP_NAME); return ObjTypes.OBJECT_KEYWORD; }
<YYINITIAL> {GROUP_KEYWORD} { yybegin(WAITING_OBJECT_OR_GROUP_NAME); return ObjTypes.GROUP_KEYWORD; }

<YYINITIAL> {VERTEX_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.VERTEX_KEYWORD; }
<YYINITIAL> {TEXTURE_COORDINATES_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.TEXTURE_COORDINATES_KEYWORD; }
<YYINITIAL> {VERTEX_NORMAL_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.VERTEX_NORMAL_KEYWORD; }

<YYINITIAL> {FACE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FACE_KEYWORD; }
<YYINITIAL> {LINE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.LINE_KEYWORD; }
<YYINITIAL> {POINT_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.POINT_KEYWORD; }

<YYINITIAL> {FREE_FORM_POINT_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_POINT_KEYWORD; }

<YYINITIAL> {FREE_FORM_TYPE_KEYWORD} { yybegin(WAITING_FREE_FORM_TYPE_NAME); return ObjTypes.FREE_FORM_TYPE_KEYWORD; }
<YYINITIAL> {FREE_FORM_DEGREE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_DEGREE_KEYWORD; }
<YYINITIAL> {FREE_FORM_BASIS_MATRIX_KEYWORD} { yybegin(WAITING_FREE_FORM_DIRECTION); return ObjTypes.FREE_FORM_BASIS_MATRIX_KEYWORD; }
<YYINITIAL> {FREE_FORM_STEP_SIZE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_STEP_SIZE_KEYWORD; }

<YYINITIAL> {FREE_FORM_CURVE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_CURVE_KEYWORD; }
<YYINITIAL> {FREE_FORM_2D_CURVE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_2D_CURVE_KEYWORD; }
<YYINITIAL> {FREE_FORM_SURFACE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_SURFACE_KEYWORD; }

<YYINITIAL> {FREE_FORM_PARAMETERS_KEYWORD} { yybegin(WAITING_FREE_FORM_DIRECTION); return ObjTypes.FREE_FORM_PARAMETERS_KEYWORD; }
<YYINITIAL> {FREE_FORM_OUTER_TRIMMING_LOOP_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_OUTER_TRIMMING_LOOP_KEYWORD; }
<YYINITIAL> {FREE_FORM_INNER_TRIMMING_LOOP_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_INNER_TRIMMING_LOOP_KEYWORD; }
<YYINITIAL> {FREE_FORM_SPECIAL_CURVE_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_SPECIAL_CURVE_KEYWORD; }
<YYINITIAL> {FREE_FORM_SPECIAL_POINTS_KEYWORD} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_SPECIAL_POINTS_KEYWORD; }

<YYINITIAL> {FREE_FORM_BODY_END_KEYWORD} { yybegin(END); return ObjTypes.FREE_FORM_BODY_END_KEYWORD; }

<YYINITIAL> {SMOOTHING_GROUP_KEYWORD} { yybegin(WAITING_SMOOTHING_GROUP_NUMBER); return ObjTypes.SMOOTHING_GROUP_KEYWORD; }

<YYINITIAL> {MATERIAL_FILE_REF_KEYWORD} { yybegin(WAITING_MATERIAL_FILE_NAME); return ObjTypes.MATERIAL_FILE_REF_KEYWORD; }
<YYINITIAL> {MATERIAL_REFERENCE_KEYWORD} { yybegin(WAITING_MATERIAL_NAME); return ObjTypes.MATERIAL_REFERENCE_KEYWORD; }

<YYINITIAL> [^\ \t\r\n\f]+ { return ObjSpecialTypes.UNKNOWN_KEYWORD; }

<WAITING_COMMENT> [^\r\n\f]*[\r\n\f] { yybegin(YYINITIAL); return ObjTypes.COMMENT; }

<WAITING_OBJECT_OR_GROUP_NAME> {OBJECT_OR_GROUP_NAME} { yybegin(END); return ObjTypes.OBJECT_OR_GROUP_NAME; }

<WAITING_FREE_FORM_TYPE_NAME> {FREE_FORM_RATIONAL_KEYWORD} { yybegin(WAITING_FREE_FORM_TYPE_NAME); return ObjTypes.FREE_FORM_RATIONAL_KEYWORD; }
<WAITING_FREE_FORM_TYPE_NAME> {FREE_FORM_TYPE_NAME} { yybegin(END); return ObjTypes.FREE_FORM_TYPE_NAME; }
<WAITING_FREE_FORM_TYPE_NAME> [^\ \t\r\n\f]+ { return ObjSpecialTypes.UNKNOWN_FREE_FORM_TYPE; }

<WAITING_FREE_FORM_DIRECTION> {FREE_FORM_DIRECTION} { yybegin(WAITING_NUMBER_OR_INDEX); return ObjTypes.FREE_FORM_DIRECTION; }
<WAITING_FREE_FORM_DIRECTION> [^\ \t\r\n\f]+ { return ObjSpecialTypes.UNKNOWN_FREE_FORM_DIRECTION; }

<WAITING_SMOOTHING_GROUP_NUMBER> {SMOOTHING_GROUP_NUMBER} { yybegin(END); return ObjTypes.SMOOTHING_GROUP_NUMBER; }
<WAITING_SMOOTHING_GROUP_NUMBER> [^\ \t\r\n\f]+ { return ObjSpecialTypes.UNKNOWN_SMOOTHING_GROUP_NUMBER; }

<WAITING_MATERIAL_FILE_NAME> {REFERENCE} { yybegin(WAITING_MATERIAL_FILE_NAME); return ObjTypes.MATERIAL_FILE_NAME; }

<WAITING_MATERIAL_NAME> {REFERENCE} { yybegin(END); return ObjTypes.MATERIAL_NAME; }

\\{CRLF} { return TokenType.WHITE_SPACE; }
{CRLF} { yybegin(YYINITIAL); return TokenType.WHITE_SPACE; }

<END> [^\ \t\r\n\f]+ { yybegin(INVALID); return TokenType.BAD_CHARACTER; }

({WHITE_SPACE})+ { return TokenType.WHITE_SPACE; }

{INDEX_SEPARATOR} { return ObjTypes.INDEX_SEPARATOR; }
{NUMBER} { return ObjTypes.NUMBER; }

[^\ \t\r\n\f\/]+ { yybegin(INVALID); return TokenType.BAD_CHARACTER; }

. { yybegin(INVALID); return TokenType.BAD_CHARACTER; }
